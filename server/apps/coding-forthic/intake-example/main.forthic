["contexts" "jira" "cache" "jinja" "intake" ["ui" ""]] USE-MODULES


contexts.JIRA-PROD jira.PUSH-CONTEXT!


# ----- Configuration ----------------------------------------------------------------------------------------
: FORM-ADMINS   ['<YOUR-USERNAME>'];
: CONFIG-URL    "<YOUR-GSHEET-URL>";


: SIMPLE-FORM-CONFIG   [
    ["tab"          "Simple"]
    ["Project"      'CF']
    ["Issue Type"   'Task']
    ["Labels"       ["forthic-intake"]]
] REC;

: FORM-CONFIGS [
    SIMPLE-FORM-CONFIG
];


# ----- CREATE-TICKET ----------------------------------------------------------------------------------------
["ticket_info"] VARIABLES

: ticket_info-PROJECT       ticket_info @ ['formConfig'  'Project'] REC@;
: ticket_info-ISSUE-TYPE    ticket_info @ ['formConfig'  'Issue Type'] REC@;
: ticket_info-SUMMARY       ticket_info @ ['valuesById'  'summary'] REC@;
: ticket_info-ATTACHMENTS   ticket_info @ ['valuesById'  'images'] REC@;

: ticket_info-LABELS   [
    ticket_info @ ['formConfig'  'Labels'] REC@
    ticket_info @ "Labels" intake.AGGREGATE-JIRA-FIELDS VALUES
] FLATTEN ">BOOL" SELECT;

# TODO: Aggregate description fields
["key" "value"] VARIABLES
: key-HEADER   ["h4. " ticket_info @ ['fieldsById' key @ "Field Label"] REC@] CONCAT;
: ticket_info-AGGREGATE-DESCRIPTION
    ticket_info @ "Description" intake.AGGREGATE-JIRA-FIELDS 'description' <DEL
    "(value ! key !) [key-HEADER value @]" !WITH-KEY MAP VALUES
;

: ticket_info-DESCRIPTION   [
    ticket_info @ ['valuesById'  'description'] REC@
    /N
    ticket_info-AGGREGATE-DESCRIPTION
] FLATTEN /N JOIN;

["ticket_key"] VARIABLES
: CREATE-TICKET   (ticket_info !)  [
        ["Project"      ticket_info-PROJECT]
        ["Issue Type"   ticket_info-ISSUE-TYPE]
        ["Summary"      ticket_info-SUMMARY]
        ["Description"  ticket_info-DESCRIPTION]
        ["Labels"       ticket_info-LABELS]
        ["Reporter"     CURRENT-USER]
    ] REC jira.CREATE (ticket_key !)
    ticket_key @ ticket_info-ATTACHMENTS jira.ADD-ATTACHMENTS
    ticket_key @;


["ticket_key"] VARIABLES
: ticket_key-DATA   [
    ["ticket_key"   ticket_key @]
    ["ticket_url"   ticket_key @ jira.ISSUE-URL]
] REC;

: CREATED-MESSAGE   (ticket_key !) '''
## Thanks for taking the time to file a ticket

Here's a link to your ticket: [{{ticket_key}}]({{ticket_url}})
''' ticket_key-DATA jinja.RENDER;

# ----- Admin -----------------------------------------------------------------------------------
# NOTE: Call this when the configs need to be updated
: REFRESH-CONFIGS!   CONFIG-URL FORM-CONFIGS FORM-ADMINS intake.REFRESH-CONFIGS!;


["str"] VARIABLES
: >JS-FRIENDLY-STRING   "\" "\\" REPLACE (str !) ["'''" str @ "'''"] CONCAT;

: UI-FORTHIC-DATA   [
    ["form_configs_json"   intake.FORM-CONFIGS >JSON >JS-FRIENDLY-STRING]
    ["is_admin"            CURRENT-USER FORM-ADMINS IN >JSON]
] REC;

# ----- UI-FORTHIC -----------------------------------------------------------------------------------
: UI-FORTHIC   """
    : FORM-CONFIGS    {{form_configs_json}} JSON>;
    : IS-ADMIN?       "{{is_admin}}" JSON>;

    ["element"] VARIABLES
    : FOR-ADMIN   (element !) [[TRUE   "element @"]] REC IS-ADMIN? REC@ INTERPRET;


    : REFRESH-CONFIGS-BUTTON   ForthicButton "Refresh Configs (Admin)" <CONTENT [["variant"  "outline-light"] ["forthic"  "[] 'REFRESH-CONFIGS!' SERVER-INTERPRET location.reload"]] REC <PROPS;

    : HOME-ELEMENT   H1 "Intake Form Home" <CONTENT;
    : FORM-ELEMENT   Div [
        Div [ H1 "Intake Form Example" <CONTENT  REFRESH-CONFIGS-BUTTON FOR-ADMIN] <CONTENT
            "text-light bg-dark bg-gradient shadow-sm p-3 mb-3 d-flex justify-content-between" <<CLASSNAME
        ConfigurableForm [
            ["form_configs"   FORM-CONFIGS]
            ["form_id_field"  "formId"]
            ["fcreated_message"  "[SWAP] 'CREATED-MESSAGE' SERVER-INTERPRET"]
        ] REC <PROPS "container w-50 shadow p-4" <<CLASSNAME
    ] <CONTENT;

    : MAIN-ROUTER   [
        "/" HOME-ELEMENT Route
        "/form" FORM-ELEMENT Route   # The root form route should pick the first configured form
        "/form/:formId" FORM-ELEMENT Route
    ] Router;
""" UI-FORTHIC-DATA jinja.RENDER;

: MAIN-PAGE   FORTHIC-REACT-v1 UI-FORTHIC <FORTHIC;
