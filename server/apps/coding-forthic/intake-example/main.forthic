["contexts" "jira" "cache" "jinja" "intake" ["ui" ""]] USE-MODULES

contexts.JIRA-PROD jira.PUSH-CONTEXT!

# ----- Configuration ----------------------------------------------------------------------------------------
# NOTE: If you're a FORM-ADMIN, we should ensure you have a Google OAuth token before we start
: FORM-ADMINS   ["rjose"];
: CONFIG-URL    "https://docs.google.com/spreadsheets/d/1hhgOLu8X6KDVb7Cw1j6Muq9EuazMVZO8cwU5e1Hz-PQ/edit#gid=0";

: FORM-CONFIGS [
    [
        ["tab"          "Coding Forthic"]
        ["Project"      "APP"]
        ["Issue Type"   "Task"]
        ["Labels"       ["forthic-intake"]]
    ] REC
];


# ----- Utils ------------------------------------------------------------------------------------------------
["str"] VARIABLES
: >JS-FRIENDLY-STRING   "\" "\\" REPLACE (str !) ["'''" str @ "'''"] CONCAT;
: |NON-NULL   ">BOOL" SELECT;
: |CONDITION-LABELS   "' ' '-' REPLACE" MAP;

# ----- CREATE-TICKET ----------------------------------------------------------------------------------------
["info"] VARIABLES  # Ticket Info
: info-FORM-CONFIG-LABELS        info @ ['formConfig'  'Labels'] REC@;
: info-AGGREGATED-LABELS         info @ "Labels" intake.AGGREGATE-JIRA-FIELDS VALUES;
: info-AGGREGATED-DESCRIPTIONS   info @ "Description" intake.AGGREGATE-JIRA-FIELDS 'description' <DEL;

: info-PROJECT      info @ ['formConfig' 'Project'] REC@;
: info-ISSUE-TYPE   info @ ['formConfig' 'Issue Type'] REC@;
: info-SUMMARY      info @ ['valuesById' 'summary'] REC@;
: info-DESCRIPTION  info @ ['valuesById' 'description'] REC@;
: info-LABELS       [info-FORM-CONFIG-LABELS info-AGGREGATED-LABELS] FLATTEN |NON-NULL |CONDITION-LABELS;
: info-ATTACHMENTS   info @ ['valuesById' 'images'] REC@;

["fieldId" "value"] VARIABLES
: fieldId-HEADER             ["h4. " info @ ['fieldsById' fieldId @ "Field Label"] REC@] CONCAT;
: info-AGGREGATE-DESCRIPTION   info-AGGREGATED-DESCRIPTIONS "(value ! fieldId !) [fieldId-HEADER value @]" !WITH-KEY MAP VALUES;
: info-FULL-DESCRIPTION        [info-DESCRIPTION /N info-AGGREGATE-DESCRIPTION] FLATTEN /N JOIN;

["ticket_key"] VARIABLES
: CREATE-TICKET   (info !)  [
    ["Project"      info-PROJECT]
    ["Issue Type"   info-ISSUE-TYPE]
    ["Summary"      info-SUMMARY]
    ["Description"  info-FULL-DESCRIPTION]
    ["Labels"       info-LABELS]
] REC jira.CREATE (ticket_key !@) info-ATTACHMENTS jira.ADD-ATTACHMENTS ticket_key @;


# ----- CREATE-MESSAGE ----------------------------------------------------------------------------------------
: CREATED-MESSAGE   (ticket_key !) '''
## Thanks for taking the time to file a ticket

Here's a link to your ticket: [{{ticket_key}}]({{ticket_url}})
''' [   ["ticket_key"   ticket_key @]
        ["ticket_url"   ticket_key @ jira.ISSUE-URL]
] REC jinja.RENDER;


# ----- Admin -----------------------------------------------------------------------------------
# NOTE: Call this when the configs need to be updated
: REFRESH-CONFIGS!   CONFIG-URL FORM-CONFIGS FORM-ADMINS intake.REFRESH-CONFIGS!;

: UI-FORTHIC-DATA   [
    ["form_configs_json"   intake.FORM-CONFIGS >JSON >JS-FRIENDLY-STRING]
    ["is_admin"            CURRENT-USER FORM-ADMINS IN >JSON]
] REC;

# ----- UI-FORTHIC -----------------------------------------------------------------------------------
: UI-FORTHIC   """
    : FORM-CONFIGS    {{form_configs_json}} JSON>;
    : IS-ADMIN?       "{{is_admin}}" JSON>;

    ["element"] VARIABLES
    : FOR-ADMIN   (element !) [[TRUE   "element @"]] REC IS-ADMIN? REC@ INTERPRET;

    : REFRESH-CONFIGS-BUTTON   ForthicButton "Refresh Configs (Admin)" <CONTENT [["variant"  "outline-light"] ["forthic"  "[] 'REFRESH-CONFIGS!' SERVER-INTERPRET location.reload"]] REC <PROPS;

    : HOME-ELEMENT   H1 "Intake Form Home" <CONTENT;
    : FORM-ELEMENT   Div [
        Div [ H1 "Simple Intake Form" <CONTENT "fs-2" <<CLASSNAME REFRESH-CONFIGS-BUTTON FOR-ADMIN] <CONTENT
            "text-light bg-dark bg-gradient shadow-sm p-3 mb-3 d-flex justify-content-between" <<CLASSNAME
        ConfigurableForm [
            ["form_configs"   FORM-CONFIGS]
            ["form_id_field"  "formId"]
            ["fcreated_message"  "[SWAP] 'CREATED-MESSAGE' SERVER-INTERPRET"]
        ] REC <PROPS "container w-50 shadow p-4" <<CLASSNAME
    ] <CONTENT;

    : MAIN-ROUTER   [
        "/" HOME-ELEMENT Route
        "/form" FORM-ELEMENT Route   # The root form route should pick the first configured form
        "/form/:formId" FORM-ELEMENT Route
    ] Router;
""" UI-FORTHIC-DATA jinja.RENDER;

: MAIN-PAGE   FORTHIC-REACT-v1 UI-FORTHIC <FORTHIC;
