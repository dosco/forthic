["contexts" "jira" "cache" "jinja" "intake" ["ui" ""]] USE-MODULES


# contexts.JIRA-PROD jira.PUSH-CONTEXT!
contexts.JIRA-STG jira.PUSH-CONTEXT!


# ----- Configuration ----------------------------------------------------------------------------------------
# : FORM-ADMINS   ['<YOUR-USERNAME>'];
: FORM-ADMINS   ['rjose'];
: CONFIG-URL    "https://docs.google.com/spreadsheets/d/1MC-WbPxnjWPMUMUvdBe-XDpK297eIr6RYBd7Bs1qJa8/edit#gid=0";

# ===== First step
: FIRST-FIELDS        ['step1' 'requestor' 'summary' 'objective' 'pain_points' 'timeline' 'business_impact' 'request_type'];
: FIRST-TRANSITIONS   [
    "ai_step" "'request_type' intake.FIELD-VALUE 'AI/Automation' ==" intake.TRANSITION
    "ds_step" "'request_type' intake.FIELD-VALUE 'DS/BI/DCOE' ==" intake.TRANSITION
];
: FIRST-STEP   "first_step" FIRST-FIELDS FIRST-TRANSITIONS intake.STEP;

# ===== AI Step
: AI-FIELDS        ["step2a" "ai_process_description"  "ai_process_details" "ai_frequency" "ai_enhancements" "ai_integration" "ai_risk"];
: AI-TRANSITIONS   NULL;
: AI-STEP      "ai_step" AI-FIELDS AI-TRANSITIONS intake.STEP;

# ===== DS Step
: DS-FIELDS        ["step2b" "ds_question" "ds_data_source" "ds_target_audience" "ds_end_users" "ds_risk" "ds_urgency"
                         "ds_project_type" "ds_update_frequency" "ds_history_depth" "ds_security_requirements"
                         "ds_attachment" "ds_solution" "ds_priority_assessment" "ds_recommended_solution"
                         "ds_solution_team" "ds_commit_date" "ds_poc"];
: DS-TRANSITIONS   NULL;
: DS-STEP      "ds_step" DS-FIELDS DS-TRANSITIONS intake.STEP;


: MY-FORM-CONFIG   [
    ["tab"          "OSI"]
    ["Project"      'OSI']
    ["Issue Type"   'Task']
    ["Labels"       ["forthic-intake"]]
    ["steps"        [FIRST-STEP AI-STEP DS-STEP]]
] REC;

: SIMPLE-FORM-CONFIG   [
    ["tab"          "Simple"]
    ["Project"      'TOOLS']
    ["Issue Type"   'Task']
    ["Labels"       ["forthic-intake"]]
] REC;

: FORM-CONFIGS [
    MY-FORM-CONFIG
    SIMPLE-FORM-CONFIG
];


# ----- CREATE-TICKET ----------------------------------------------------------------------------------------
["ticket_info"] VARIABLES

: ticket_info-PROJECT                  ticket_info @ ['formConfig' 'Project'] REC@;
: ticket_info-ISSUE-TYPE               ticket_info @ ['formConfig' 'Issue Type'] REC@;

: ticket_info-SUMMARY                  ticket_info @ ['valuesById' 'summary'] REC@;
: ticket_info-ATTACHMENTS              ticket_info @ ['valuesById' 'images'] REC@;
# : ticket_info-PROBLEM                  ticket_info @ ['valuesById' 'problem'] REC@;
: ticket_info-BUSINESS-CASE            ticket_info @ ['valuesById' 'pain_points'] REC@;
: ticket_info-NEED-BY                  ticket_info @ ['valuesById' 'timeline'] REC@;
# : ticket_info-BUSINESS-IMPACT          ticket_info @ ['valuesById' 'business_impact'] REC@;
: ticket_info-BENEFITS                 ticket_info @ ['valuesById' 'business_value'] REC@;

: ticket_info-PRIORITY                 ticket_info @ ['valuesById' 'ds_urgency'] REC@;
: ticket_info-CONTRIBUTING-FACTORS     ticket_info @ ['valuesById' 'ds_project_type'] REC@;
: ticket_info-EXPECTED-DELIVERY-DATE   ticket_info @ ['valuesById' 'ds_commit_date'] REC@;

: ticket_info-LABELS   [
    ticket_info @ ['formConfig' 'Labels'] REC@
    ticket_info @ "Labels" intake.AGGREGATE-JIRA-FIELDS VALUES
] FLATTEN ">BOOL" SELECT;

: ticket_info-COMPONENTS   [
    ticket_info @ "Component/s" intake.AGGREGATE-JIRA-FIELDS VALUES
] FLATTEN ">BOOL" SELECT;

["key" "value"] VARIABLES
: key-HEADER   ["h4. " ticket_info @ ['fieldsById' key @ "Field Label"] REC@] CONCAT;
: ticket_info-AGGREGATE-DESCRIPTION
    ticket_info @ "Description" intake.AGGREGATE-JIRA-FIELDS 'description' <DEL
    "(value ! key !) [key-HEADER value @]" !WITH-KEY MAP VALUES
;

: ticket_info-DESCRIPTION   [
    ticket_info @ ['valuesById'  'description'] REC@
    /N
    ticket_info-AGGREGATE-DESCRIPTION
] FLATTEN /N JOIN;

["ticket_key"] VARIABLES
: CREATE-TICKET   (ticket_info !)  [
        ["Project"      ticket_info-PROJECT]
        ["Issue Type"   ticket_info-ISSUE-TYPE]
        ["Summary"      ticket_info-SUMMARY]
        ["Description"  ticket_info-DESCRIPTION]
        # ["Problem"      ticket_info-PROBLEM]

        ["Business Case"           ticket_info-BUSINESS-CASE]
        ["Need by"                 ticket_info-NEED-BY]
        # ["Business Impact"         ticket_info-BUSINESS-IMPACT]
        ["Benefits"                ticket_info-BENEFITS]
        ["Priority"                ticket_info-PRIORITY]
        ["Contributing Factors"    ticket_info-CONTRIBUTING-FACTORS]
        ["Expected Delivery Date"  ticket_info-EXPECTED-DELIVERY-DATE]

        ["Labels"       ticket_info-LABELS]
        # ["Component/s"  ticket_info-COMPONENTS]
        ["Reporter"     CURRENT-USER]
    ] REC ">BOOL" SELECT jira.CREATE (ticket_key !)
    ticket_key @ ticket_info-ATTACHMENTS jira.ADD-ATTACHMENTS
    ticket_key @;

# "sample_ticket" cache.CACHE@ ">BOOL" SELECT .s jira.CREATE .s


["ticket_key"] VARIABLES
: ticket_key-DATA   [
    ["ticket_key"   ticket_key @]
    ["ticket_url"   ticket_key @ jira.ISSUE-URL]
] REC;

: CREATED-MESSAGE   (ticket_key !) '''
## Thanks for taking the time to file a ticket

Here's a link to your ticket: [{{ticket_key}}]({{ticket_url}})
''' ticket_key-DATA jinja.RENDER;

# ----- Admin -----------------------------------------------------------------------------------
# NOTE: Call this when the configs need to be updated
: REFRESH-CONFIGS!   CONFIG-URL FORM-CONFIGS FORM-ADMINS intake.REFRESH-CONFIGS!;


["str"] VARIABLES
: >JS-FRIENDLY-STRING   "\" "\\" REPLACE (str !) ["'''" str @ "'''"] CONCAT;

: UI-FORTHIC-DATA   [
    ["form_configs_json"   intake.FORM-CONFIGS >JSON >JS-FRIENDLY-STRING]
    ["is_admin"            CURRENT-USER FORM-ADMINS IN >JSON]
] REC;

# ----- UI-FORTHIC -----------------------------------------------------------------------------------
: UI-FORTHIC   """
    : FORM-CONFIGS    {{form_configs_json}} JSON>;
    : IS-ADMIN?       "{{is_admin}}" JSON>;

    ["element"] VARIABLES
    : FOR-ADMIN   (element !) [[TRUE   "element @"]] REC IS-ADMIN? REC@ INTERPRET;


    : REFRESH-CONFIGS-BUTTON   ForthicButton "Refresh Configs (Admin)" <CONTENT [["variant"  "outline-light"] ["forthic"  "[] 'REFRESH-CONFIGS!' SERVER-INTERPRET location.reload"]] REC <PROPS;

    : HOME-ELEMENT   H1 "Intake Form Home" <CONTENT;
    : FORM-ELEMENT   Div [
        Div [ H1 "Intake Form Example" <CONTENT  REFRESH-CONFIGS-BUTTON FOR-ADMIN] <CONTENT
            "text-light bg-dark bg-gradient shadow-sm p-3 mb-3 d-flex justify-content-between" <<CLASSNAME
        ConfigurableForm [
            ["form_configs"   FORM-CONFIGS]
            ["form_id_field"  "formId"]
            ["fcreated_message"  "[SWAP] 'CREATED-MESSAGE' SERVER-INTERPRET"]
        ] REC <PROPS "container w-50 shadow p-4" <<CLASSNAME
    ] <CONTENT;

    : MAIN-ROUTER   [
        "/" HOME-ELEMENT Route
        "/form" FORM-ELEMENT Route   # The root form route should pick the first configured form
        "/form/:formId" FORM-ELEMENT Route
    ] Router;
""" UI-FORTHIC-DATA jinja.RENDER;

: MAIN-PAGE   FORTHIC-REACT-v1 UI-FORTHIC <FORTHIC;
